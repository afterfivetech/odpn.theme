<?xml version="1.0" encoding="UTF-8"?>
<rules
  xmlns="http://namespaces.plone.org/diazo"
  xmlns:css="http://namespaces.plone.org/diazo/css"
  xmlns:xsl="http://www.w3.org/1999/XSL/Transform">


  <theme href="index.html"/>
  <notheme css:if-not-content="#visual-portal-wrapper" />
  
  <rules css:if-content="#portal-top">
    <!-- Attributes -->
    <copy attributes="*" css:theme="html" css:content="html" />
    <!-- Base tag -->
    <before css:theme="head title" css:content="head base" />
    <!-- Title -->
    <replace css:theme="head title" css:content="head title" />
    <!-- Pull in Plone Meta -->
    <after css:theme-children="head" css:content="head meta" />
    <!-- dont use Plone icons, use the theme's -->
    <drop css:content="head link[rel='apple-touch-icon'], head link[rel='shortcut icon']" />
    <!-- CSS -->
    <after css:theme-children="head" css:content="head link" />
    <!-- Script -->
    <after css:theme-children="head" css:content="head script" />
  </rules>
  
  <!-- Copy over the id/class attributes on the body tag.
       This is important for per-section styling -->
  <copy attributes="*" css:content="body" css:theme="body" />
  
  <!-- toolbar -->
  <before
    css:theme-children="body"
    css:content-children="#edit-bar"
    css:if-not-content=".ajax_load"
    css:if-content=".userrole-authenticated"
    />
  <!--branding-->
  <replace css:theme-children=".branding" css:content="#portal-logo" />

  <!-- move global nav -->
  <replace css:theme-children=".main-menu" css:content-children="#portal-globalnav" />
  
  <!--login-->
  <after css:theme=".search-trigger" css:if-content="#portal-anontools">
    <xsl:if test="//div[@id='portal-anontools']/*">
      <xsl:element name="a">
        <xsl:attribute name="href">
          <xsl:value-of select="//a[@id='personaltools-login']/@href" />
        </xsl:attribute>
        <xsl:attribute name="data-pat-plone-modal">
          <xsl:value-of select="//a[@id='personaltools-login']/@data-pat-plone-modal" />
        </xsl:attribute>
        <xsl:attribute name="id">
          <xsl:value-of select="//a[@id='personaltools-login']/@id" />
        </xsl:attribute>
        <xsl:attribute name="class">
          <xsl:value-of select="//a[@id='personaltools-login']/@class" />
        </xsl:attribute>
        <xsl:attribute name="title">
          <xsl:value-of select="//a[@id='personaltools-login']/@title" />
        </xsl:attribute>
        <span class="hb hb-xs hb-custom hb-tencent-weibo">
          <i class="fa fa-sign-in" aria-hidden="true"></i>
        </span>
      </xsl:element>
    </xsl:if>
  </after>
  
  <!--search-->
  <replace css:theme-children=".modal-body" css:content-children="#portal-searchbox" />
  
  <!-- front-page slider -->
  <drop
    css:theme="#main-slider"
    css:if-not-content=".section-front-page" />
  <drop
    css:theme="#main-slider"
    css:if-content=".template-edit" />
  <drop
    css:theme="#main-slider"
    css:if-content=".template-topbar-manage-portlets" />
  
  <!-- dynamic slider content -->
  <replace
    css:theme="#carousel-example-generic"
    css:content="#carousel-example-generic"
    href="/slider-images/@@slider-images" />
  
  <!--drop mainpage portlet-->
  <drop
    css:content="#mainpage-portlet" />
  
  <!--replace main container child if mainpage portlet-->
  <replace
    css:theme-children="#main-container"
    css:if-content="#mainpage-portlet">
    <xsl:copy-of css:select="#mainpage-portlet"/>
  </replace>
  
  <!--breadcrums-->
  <drop
    css:theme="#breadcrumbs-container"
    css:if-content=".template-document_view.section-front-page" />
  <replace css:theme-children="#breadcrumbs-container > div > .breadcrumb"
           css:if-not-content=".template-document_view.section-front-page">
        <xsl:copy-of css:select="#portal-breadcrumbs > div > ol > li"/>
  </replace>
  
  <!-- Central column -->
  <replace css:theme="#content-container" method="raw">

      <xsl:variable name="central">
        <xsl:if test="//aside[@id='portal-column-one'] and //aside[@id='portal-column-two']">col-xs-12 col-sm-6</xsl:if>
        <xsl:if test="//aside[@id='portal-column-two'] and not(//aside[@id='portal-column-one'])">col-xs-12 col-sm-9</xsl:if>
        <xsl:if test="//aside[@id='portal-column-one'] and not(//aside[@id='portal-column-two'])">col-xs-12 col-sm-9</xsl:if>
        <xsl:if test="not(//aside[@id='portal-column-one']) and not(//aside[@id='portal-column-two'])">col-xs-12 col-sm-12</xsl:if>
      </xsl:variable>

      <div class="{$central}">
        <div class="row">
            <div class="col-xs-12 col-sm-12">
              <xsl:apply-templates css:select="#content" />
            </div>
            <div class="clearFix"></div>
        </div>
        <section id="viewlet-below-content-body" class="row">
            <div class="col-xs-12 col-sm-12">
             <xsl:copy-of css:select="#viewlet-below-content" />
            </div>
            <div class="clearFix"></div>
        </section>
      </div><!--/row-->
  </replace>

  <!-- Left column -->
  <rules css:if-content="#portal-column-one">
    <replace css:theme="#column1-container">
        <div id="column1-container" class="col-xs-12 col-sm-3 sidebar-offcanvas">
          <aside id="portal-column-one">
             <xsl:copy-of css:select="#portal-column-one > *" />
          </aside>
        </div>
    </replace>
  </rules>

  <!-- Right column -->
  <rules css:if-content="#portal-column-two">
    <replace css:theme="#column2-container">
        <div id="column2-container" class="col-xs-12 col-sm-3 sidebar-offcanvas" role="complementary">
          <aside id="portal-column-two">
             <xsl:copy-of css:select="#portal-column-two > *"/>
          </aside>
        </div>
    </replace>
  </rules>
  
  <!--Footer-->
  <replace css:theme-children="#footer-top > div">
    <xsl:for-each css:select="#portal-footer-wrapper > div > div > .portletWrapper">
      <div class="ftop col-md-3">
        <xsl:copy-of css:select=".portletContent"/>
      </div>
    </xsl:for-each>
    
  </replace>
  
  
</rules>
